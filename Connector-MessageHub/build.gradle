/*******************************************************************************
 * Copyright (c) 2015 IBM Corp.
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * and Eclipse Distribution License v1.0 which accompany this distribution. 
 *
 * The Eclipse Public License is available at 
 *    http://www.eclipse.org/legal/epl-v10.html
 * and the Eclipse Distribution License is available at 
 *   http://www.eclipse.org/org/documents/edl-v10.php.
 *
 * Contributors:
 *    Leo Davison - initial implementation
 */

apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'com.ibm.iotf.connector.Connector'
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'

repositories {
    mavenCentral()

	// read from lib/ directory
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	compile 'org.apache.kafka:kafka-clients:0.9.0.0'
	compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.0-rc1'
	compile 'com.google.code.gson:gson:2.2.4'
	compile 'org.eclipse.paho:org.eclipse.paho.mqtt.utility:1.0.2'
	compile fileTree(dir: 'lib-message-hub', include: ['*.jar'])	
}

compileJava {

    doFirst {
        copy {
            from configurations.runtime
            into 'lib'
        }

        copy {
            from 'lib-message-hub'
            into 'lib'
        }
    }

	source = 'src'
    options.encoding = 'ISO-8859-1'
}

jar {   
	doLast {
		copy {
            from 'resources'
            into 'build/libs/resources'
            exclude 'log4j.properties'
        }

        copy {
            from 'resources/log4j.properties'
            into 'build/libs'
        }

		copy {
            from 'lib'
            into 'build/libs/lib'
        }
	}
    // Provide Main-Class so the JAR executes properly, along with the required classpath data.
    manifest {
        attributes('Main-Class': mainClassName,
        'Class-Path': '. ./ ./lib ' + configurations.runtime.files.collect { './lib/' + it.name }.join(' '))
    }
}

// Save JAR in build directory.
uploadArchives {
    repositories {
        flatDir {
            dirs 'build'
        }
    }
}

['Zip', 'Tar'].each { suffix ->
    "dist$suffix" {
        def basePath = baseName + '-' + version

        from('resources') {
            into { basePath + '/bin/resources' }
            exclude 'log4j.properties'
        }    

        from('resources') {
            into { basePath + '/lib' }
            include 'log4j.properties'
        }
    }
}